hansfriese

medium

# In consistent modification of `_bridgeTxData` in `HardenedTopupProxy._processTopup()`.

## Summary
In consistent modification of `_bridgeTxData` in `HardenedTopupProxy._processTopup()`.

## Vulnerability Detail
The function `_processTopup()` is used to exchange any tokens to `cardTopupToken` and bridge to card L1.

When the input token is the same as the `cardTopupToken`, it doesn't use the `ExchangeProxy` and bridges directly [here](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L315-L326) without changing `_bridgeTxData`.

```solidity
    if (_token == cardTopupToken) {
        // beneficiary is msg.sender (perform static check)
        IERC20Upgradeable(_token).safeTransferFrom(_beneficiary, address(this), _amount);

        uint256 feeAmount = _amount.mul(topupFee).div(1e18);

        // bridge from _beneficiary to card L1 relay
        _bridgeAssetDirect(_amount.sub(feeAmount), _bridgeType, _bridgeTxData);

        emit CardTopup(_beneficiary, _token, _amount, _amount.sub(feeAmount), _receiverHash);
        return;
    }
```

But when it works with other tokens, it modifies the `_bridgeTxData` after exchange [here](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L345-L377).

```solidity
    // this is sanity check from the client if the swap misbehaves
    require(amountReceived >= _expectedMinimumReceived, "minimum swap amount not met");

    // fee is deducted in receiving token (USDC)
    if (topupFee != 0) {
        uint256 feeAmount = amountReceived.mul(topupFee).div(1e18);
        amountReceived = amountReceived.sub(feeAmount);
    }

    if (_bridgeType == 0) {
        // if using Synapse bridge and a swap was performed
        // because of Synapse bridge interface is off-chain
        // modify part of bridgeTxData to reflect new amount
        // in bridgeTxData, its layout is as following:
        // bytes   0..19 target address (topup relay on L1)
        // bytes  20..23 function signature
        // bytes  24..151 bridge tx params
        // bytes 152..183 min to mint
        // bytes 184..279 bridge tx params
        // bytes 280..311 min dy
        // bytes 312..407 bridge tx params
        // bytes 408..439 source amount
        // bytes 440..471 bridge tx params
        uint256 minMint = amountReceived.mul(950000).div(1000000); // 0.95 nUSD to mint
        uint256 minDy = amountReceived.mul(910000).div(1000000); // 0.91 expected to be received
        assembly {
            // first 32 bytes of 'bytes' is it's length, and after that it's contents
            // so offsets are 32+152=184, 32+280=312, 32+408=440
            mstore(add(_bridgeTxData, 184), minMint)
            mstore(add(_bridgeTxData, 312), minDy)
            mstore(add(_bridgeTxData, 440), amountReceived)
        }
    }
```

As we can see, it recalculates the `_bridgeTxData` after deducting the topupFee.

But when the input token is same as the `cardTopupToken`, it doesn't update `_bridgeTxData` although it deducts a fee [here](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L322).

```solidity
_bridgeAssetDirect(_amount.sub(feeAmount), _bridgeType, _bridgeTxData);
```

I think `_bridgeTxData` should be modified for this case as well because `_amount` is changed.

## Impact
`_processTopup()` will use a different amount of `_bridgeTxData` when the input token is the same as `cardTopupToken` or not and it might bring an unexpected outcome.

## Code Snippet
https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L322

## Tool used
Manual Review

## Recommendation
We should modify `_bridgeTxData` with `_amount.sub(feeAmount)` using [this formula](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L354-L377).
