hansfriese

high

# Users can exchange tokens to ETH or other ERC777 tokens without paying `exchangeFee`.

## Summary
Users can exchange tokens to ETH or other ERC777 tokens without paying `exchangeFee`.

## Vulnerability Detail
Currently, users can exchange any tokens to `cardTopupToken` using `ExchangeProxy.executeSwapDirect()` and they should pay the `exchangeFee`.

```solidity
    function executeSwapDirect(
        address _beneficiary,
        address _tokenFrom,
        address _tokenTo,
        uint256 _amount,
        uint256 _exchangeFee,
        bytes memory _data
    ) public payable override returns (uint256) {
        require(msg.sender == transferProxyAddress, "transfer proxy only");

        // extract values from bytes array provided
        address executorAddress;
        address spenderAddress;
        uint256 ethValue;

        bytes memory callData = ByteUtil.slice(_data, 72, _data.length - 72);
        assembly {
            executorAddress := mload(add(_data, add(0x14, 0)))
            spenderAddress := mload(add(_data, add(0x14, 0x14)))
            ethValue := mload(add(_data, add(0x20, 0x28)))
        }

        // allow spender to transfer tokens from this contract
        if (_tokenFrom != ETH_TOKEN_ADDRESS && spenderAddress != address(0)) {
            require(trustedRegistryContract.isWhitelisted(spenderAddress), "allowance to non-trusted");
            resetAllowanceIfNeeded(IERC20(_tokenFrom), spenderAddress, _amount);
        }

        // remember the actual balance of target token (fees could reside on this contract balance)
        uint256 balanceBefore = 0;
        if (_tokenTo != ETH_TOKEN_ADDRESS) {
            balanceBefore = IERC20(_tokenTo).balanceOf(address(this));
        } else {
            balanceBefore = address(this).balance;
        }

        // regardless of stated amount, the ETH value passed to exchange call must be provided to the contract
        require(msg.value >= ethValue, "insufficient ETH provided");

        // don't allow to call non-trusted addresses
        require(trustedRegistryContract.isWhitelisted(executorAddress), "call to non-trusted");

        // ensure no state passed, no reentrancy, etc.
        (bool success, ) = executorAddress.call{value: ethValue}(callData);
        require(success, "SWAP_CALL_FAILED");

        // always rely only on actual amount received regardless of called parameters
        uint256 amountReceived = 0;
        if (_tokenTo != ETH_TOKEN_ADDRESS) {
            amountReceived = IERC20(_tokenTo).balanceOf(address(this));
        } else {
            amountReceived = address(this).balance;
        }
        amountReceived = amountReceived.sub(balanceBefore);

        require(amountReceived > 0, "zero amount received");

        // process exchange fee if present (in deposit we get pool tokens, so process fees after swap, here we take fees in source token)
        // fees are left on this contract address and are harvested by yield distributor
        //uint256 feeAmount = amountReceived.mul(_exchangeFee).div(1e18);
        amountReceived = amountReceived.sub(
            amountReceived.mul(_exchangeFee).div(1e18)
        ); // this is return value that should reflect actual result of swap (for deposit, etc.)

        if (_tokenTo != ETH_TOKEN_ADDRESS) {
            //send received tokens to beneficiary directly
            IERC20(_tokenTo).safeTransfer(_beneficiary, amountReceived);
        } else {
            //send received eth to beneficiary directly
            payable(_beneficiary).sendValue(amountReceived);
            // payable(_beneficiary).transfer(amountReceived);
            // should work for external wallets (currently is the case)
            // but wont work for some other smart contracts due to gas stipend limit
        }

        emit ExecuteSwap(_beneficiary, _tokenFrom, _tokenTo, _amount, amountReceived);
        
        // amount received is used to check minimal amount condition set by calling app from topup proxy contract
        return amountReceived;
    }
```

But it doesn't validate the exchange data properly and users can use this function by paying small fees than they should.

- Let's assume `cardTopupToken = USDC`, `exchangeFee = 10%`
- Alice calls `executeSwapDirect()` with `_tokenFrom = DAI`, `_tokenTo = USDC`, `amount = 100`, `_exchangeFee = 10%`.
- Normally it should exchange from `DAI` to `USDC`,but she sends a different [_data](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/ExchangeProxy.sol#L137) to exchange from `DAI` to `ETH`, transfer to herself(not this contract).
- Like the [1inch protocol](https://github.com/1inch/liquidity-protocol/blob/master/contracts/Mooniswap.sol#L255), it's possible to set a `receiver` to get the output token after the swap for normal liquidity providers.
- As there is no validation of the output token, [the swap](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/ExchangeProxy.sol#L173-L175) will be executed successfully.

```solidity
    // ensure no state passed, no reentrancy, etc.
    (bool success, ) = executorAddress.call{value: ethValue}(callData);
    require(success, "SWAP_CALL_FAILED");
```
- So the exchanged ETH will be sent to Alice(not the proxy contract) and she can transfer a small amount of `USDC` (like `HardenedTopupProxy.minAmount + fee`) to this proxy contract from the `receive()` function of her contract.
- Then [this condition](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/ExchangeProxy.sol#L184) will be met and it will pass [this requirement](https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/HardenedTopupProxy.sol#L396) also.
- This scenario is possible for other tokens with fallback functions like ERC777 tokens.

After all, users can exchange any tokens for ETH or ERC777 tokens by paying a very small fee of the `minAmount`.

## Impact
Users can bypass the `exchangeFee` during the swap.

## Code Snippet
https://github.com/sherlock-audit/2022-10-mover/blob/main/cardtopup_contract/contracts/ExchangeProxy.sol#L131-L210

## Tool used
Manual Review

## Recommendation
Unfortunately, I don't have any method to prevent this approach.

I think it would be ok if the protocol charges some percentage of input tokens before the swap.